PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX geo: <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX schema: <https://schema.org/>
PREFIX mm: <http://movemadrid.org/def/base#>

# Query 1: Count the total number of Bike Parking resources
SELECT (COUNT(?parking) AS ?totalBikeParkings)
WHERE {
    ?parking a mm:BikeParking .
}

# Query 2: Find the coordinates and ID of all Bike Parking spots in the 'Ciudad Lineal' district (COD_DISTRITO 15)
SELECT ?parkingID ?lat ?long
WHERE {
  ?parking a mm:BikeParking ;
           geo:lat ?lat ;
           geo:long ?long ;
           mm:inDistrict <http://movemadrid.org/def/base#District/15> .
           
  BIND(STRAFTER(STR(?parking), "BikeParking/") AS ?parkingID)
}
LIMIT 10

# Query 3: Find the full street address for a specific parking spot and its postal code
SELECT ?streetAddress ?postalCode
WHERE {
  <http://movemadrid.org/def/base#BikeParking/101532> schema:address ?addressResource .
  
  ?addressResource a schema:PostalAddress ;
                   schema:streetAddress ?streetAddress ;
                   schema:postalCode ?postalCode .
}

# Query 4: Count number of Bike Parkings per District
SELECT ?districtName (COUNT(?parking) AS ?parkingCount)
WHERE {
    ?parking a mm:BikeParking ;
             mm:inDistrict ?district .
    ?district schema:name ?districtName .
}
GROUP BY ?districtName
ORDER BY DESC(?parkingCount)
